Operator welp : ().
[welp] =def= [unit].

Theorem test1 : [Σ(unit; _. Σ(unit; _. unit))] {
  *{ intro ; auto }.
}.

||| Lemmas defined earlier in the development may be used later on using the 'lemma' tactic.
Theorem test1' : [Σ(unit; _. Σ(unit; _. unit))] {
  lemma <test1>.
}.

||| Abstractions may be unfolded using the 'unfold' tactic.
Theorem test2 : [Π(unit; _. Σ(unit; _. welp))] {
  unfold <welp>; auto
}.

Theorem test3 : [∈(λ(x. x); Π(unit; _. unit))] {
  auto.
}.

Theorem test4 : [∈(λ(x.pair(x;x)); Π(void;_.void))] {
  auto.
}.

Theorem test5 : [Π(void; _. Σ(unit; _.unit))] {
  auto.
}.

Theorem test6 : [Π(unit; _. Σ(unit; _.unit))] {
  witness [λ(x. pair(x;x))]; auto.
}.

Theorem test7 : [Π(Σ(void;_.unit); z. void)] {
  auto;
  elim #1;
  auto.
}.

Theorem test8 : [∈(+(void; void); U{i})] {
  auto
}.

Theorem test9 : [=(inl(<>); inl(<>); +(unit; void))] {
  auto
}.

Theorem test10 : [∀(U{i}; A.
                  ∀(U{i}; B.
                  Π(+(A; B); _. +(B; A))))] {
  auto; elim #3; [intro #1, intro #0]; auto
}.

Theorem test11 :
        [=(decide(inl(<>); x.x; y.y); decide(inr(<>); x.x; y.y); unit)] {
  reduce; auto
}.

Theorem axiom-of-choice : [∀(U{i}; A. ∀(U{i}; B. ∀(Π(A; _. Π(B; _. U{i})); Q. Π(Π(A; a. Σ(B; b. ap(ap(Q;a);b))); φ. Σ(Π(A; _.B); f. Π(A; a. ap(ap(Q;a);ap(f;a))))))))] {
  auto; intro [λ(w. spread(ap(φ;w); x.y.x))]; auto;
  elim #4 [a]; auto; reduce;
  hyp-subst ← #7 [h. ap(ap(Q;a); spread(h; x.y.x))]; auto;
  elim #6; reduce; auto
}.
