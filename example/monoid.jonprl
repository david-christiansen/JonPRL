Operator MonoidSig : ().
Operator car : (0).
Operator ze : (0).
Operator op : (0;0;0).

Operator MonoidLaws : (0).
Operator LeftUnit : (0).
Operator RightUnit : (0).
Operator Assoc : (0).

Operator Monoid : ().

[MonoidSig] =def= [Σ(U{i}; A. Σ(A; zero. Π(A; m. Π(A; n. A))))].
[car(M)] =def= [spread(M; x.y.x)].
[ze(M)] =def= [spread(spread(M; x.y.y); x.y.x)].
[op(M;X;Y)] =def= [ap(ap(spread(spread(M; x.y.y); x.y.y);X);Y)].
[LeftUnit(M)] =def= [⋂(car(M); m. =(m; op(M; ze(M); m); car(M)))].
[RightUnit(M)] =def= [⋂(car(M); m. =(op(M; ze(M); m); m; car(M)))].
[Assoc(M)] =def= [⋂(car(M); x. ⋂(car(M); y. ⋂(car(M); z. =(op(M;op(M;x;y);z); op(M;x;op(M;y;z));car(M)))))].

[MonoidLaws(M)] =def= [Σ(LeftUnit(M); _. Σ(RightUnit(M); _. Assoc(M)))].
[Monoid] =def= [subset(MonoidSig; M. MonoidLaws(M))].

Tactic monoid-sig-unfold {
  unfold <MonoidSig car op ze>
}.

Theorem MonoidSig-wf : [∈(MonoidSig; U{i'})] {
  refine <monoid-sig-unfold>; auto.
}.

Theorem car-wf : [⋂(MonoidSig; M. ∈(car(M); U{i}))] {
  refine <monoid-sig-unfold>; auto.
}.

Tactic monoid-laws-unfold {
  unfold <MonoidLaws LeftUnit RightUnit Assoc>
}.

Tactic monoid-unfold {
  unfold <Monoid>; refine <monoid-sig-unfold>; refine <monoid-laws-unfold>.
}.

Tactic monoid-simplify {
  *{ refine <monoid-unfold>; reduce; auto }.
}.

Theorem LeftUnit-wf : [⋂(MonoidSig; M. ∈(LeftUnit(M); U{i}))] {
  refine <monoid-simplify>.
}.

Theorem RightUnit-wf : [⋂(MonoidSig; M. ∈(RightUnit(M);U{i}))] {
  refine <monoid-simplify>.
}.

Theorem Assoc-wf : [⋂(MonoidSig; M. ∈(Assoc(M); U{i}))] {
  refine <monoid-simplify>.
}.

Theorem MonoidLaws-wf : [⋂(MonoidSig; M. ∈(MonoidLaws(M); U{i}))] {
  refine <monoid-simplify>.
}.

Theorem Monoid-wf : [∈(Monoid; U{i'})] {
  refine <monoid-simplify>.
}.

Theorem UnitMonoidStruct : [MonoidSig] {
  unfold <MonoidSig>;
  intro [unit]; auto;
  intro [<>] ; auto.
}.

Theorem UnitMonoid : [Monoid] {
  unfold <Monoid>;
  intro [UnitMonoidStruct] @i; unfold <UnitMonoidStruct>;
  refine <monoid-simplify>;
  elim #1; auto.
}.

Theorem UnitMonoid-LeftUnit : [LeftUnit(UnitMonoidStruct)] {
  unfold <UnitMonoidStruct>;
  refine <monoid-simplify>; elim #1; auto.
}.

Theorem UnitMonoid-RightUnit : [RightUnit(UnitMonoidStruct)] {
  unfold <UnitMonoidStruct>;
  refine <monoid-simplify>; elim #1; auto.
}.
