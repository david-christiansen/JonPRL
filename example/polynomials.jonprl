Operator fst : (0).
Operator snd : (0).

[fst(p)] =def= [spread(p; x.y.x)].
[snd(p)] =def= [spread(p; x.y.y)].

||| Ambient category

Operator CTT : (0;0).

[CTT(A;B)] =def= [Π(A; _. B)].

||| Power / Copower

Operator Power : (0;0).
Operator Copower : (0;0).

[Power(A;B)] =def= [Π(A; _. B)].
[Copower(A;B)] =def= [Σ(A; _. B)].

||| End / Coend

Operator End : (0).
Operator Coend : (0).

[End(Hom)] =def= [Π(U<0>; A. ap(ap(Hom; A); A))].
[Coend(Hom)] =def= [Σ(U<0>; A. ap(ap(Hom; A); A))].

Theorem End_wf : [
  ∀(Π(U<0>; _. Π(U<0>; _. U<0>)); Hom. ∈(End(Hom); U<1>))
] {
  unfold <End>; auto.
}.
Theorem Coend_wf : [
  ∀(Π(U<0>; _. Π(U<0>; _. U<0>)); Hom. ∈(Coend(Hom); U<1>))
] {
  unfold <Coend>; auto.
}.

||| Ran / Lan

Operator Ran : (0;0;0).
Operator Lan : (0;0;0).

[Ran(G;H;A)] =def= [End(λ(X. λ(Y. Power(CTT(A; ap(G;X)); ap(H;Y)))))].
[Lan(G;H;A)] =def= [Coend(λ(X. λ(Y. Copower(CTT(ap(G;X); A); ap(H;Y)))))].

Theorem Ran_wf : [
  ∀(Π(U<0>; _. U<0>); G.
  ∀(Π(U<0>; _. U<0>); H.
  ∀(U<0>; A.
    ∈(Ran(G;H;A); U<1>))))
] {
  unfold <Ran Power End CTT>; auto.
}.
Theorem Lan_wf : [
  ∀(Π(U<0>; _. U<0>); G.
  ∀(Π(U<0>; _. U<0>); H.
  ∀(U<0>; A.
    ∈(Lan(G;H;A); U<1>))))
] {
  unfold <Lan Copower Coend CTT>; auto.
}.

||| Fam

Operator Fam : (0).
Operator dom : (0).
Operator map : (0;0).

[Fam(B)] =def= [Σ(U<0>; Dom. Π(Dom; _. B))].
[dom(f)] =def= [fst(f)].
[map(f;b)] =def= [ap(snd(f); b)].

Theorem Fam_wf : [∀(U<0>; B. ∈(Fam(B); U<1>))] {
  unfold <Fam>; auto
}.
Theorem dom_wf : [∀(U<0>; B. ∀(Fam(B); f. ∈(dom(f); U<0>)))] {
  unfold <Fam map dom fst>; auto
}.
Theorem map_wf : [
  ∀(U<0>; B. ∀(Fam(B); f. ∀(dom(f); x. ∈(map(f;x); B))))
] {
  unfold <Fam dom map fst snd>; auto.
}.

||| Fibers of a bundle over a point

Operator Fiber : (0;0;0).

[Fiber(B; f; b)] =def= [Σ(dom(f); x. =(b; map(f;x); B))].

Theorem Fiber_wf : [
  ∀(U<0>; B. ∀(Fam(B); f. ∀(B; b. ∈(Fiber(B;f;b); U<0>))))
] {
  unfold <Fam Fiber dom map fst snd>; auto.
}.

||| Quantifier adjoints

Operator Exists : (0;0;0).
Operator Invert : (0).
Operator Forall : (0;0;0).

||| Exists ⊣ Invert ⊣ Forall

[Exists(X;Y;f)] =def= [λ(φ. λ(y. Σ(X; x. Σ(=(ap(f;x); y; Y); _. ap(φ;x)))))].
[Invert(f)] =def= [λ(φ. λ(x. ap(φ;ap(f;x))))].
[Forall(X;Y;f)] =def= [λ(φ. λ(y. Σ(X; x. Π(=(y; ap(f;x); Y); _. ap(φ;x)))))].

Theorem Exists_wf : [
  ∀(U<0>; X. ∀(U<0>; Y. ∀(Π(X; _. Y); f. ∀(Π(X; _. U<0>); φ.
    ∈(ap(Exists(X;Y;f);φ); Π(Y; _. U<0>))))))
] {
  unfold <Exists>; auto.
}.
Theorem Invert_wf : [
  ∀(U<0>; X. ∀(U<0>; Y. ∀(Π(X; _. Y); f. ∀(Π(Y; _. U<0>); φ.
    ∈(ap(Invert(f);φ); Π(X; _. U<0>))))))
] {
  unfold <Invert>; auto.
}.
Theorem Forall_wf : [
  ∀(U<0>; X. ∀(U<0>; Y. ∀(Π(X; _. Y); f. ∀(Π(X; _. U<0>); φ.
    ∈(ap(Forall(X;Y;f);φ); Π(Y; _. U<0>))))))
] {
  unfold <Forall>; auto.
}.

||| Polynomials (non-indexed)

Operator Polynomial : ().
Operator base : (0).
Operator fam : (0).

[Polynomial] =def= [Σ(U<0>; Base. Fam(Base))].
[base(E)] =def= [fst(E)].
[fam(E)] =def= [snd(E)].

Theorem Polynomial_wf : [∈(Polynomial; U<1>)] {
  unfold <Polynomial Fam>; auto
}.

||| Yoneda embedding

Operator Embed : (0).

[Embed(B)] =def= [λ(A. CTT(A;B))].

Theorem Embed_wf : [
  ∀(U<0>; B. ∀(U<0>; X. ∈(ap(Embed(B); X); U<0>)))
] {
  unfold <Embed CTT>; auto.
}.

||| Extension functors of polynomials

Operator Ext : (0;0).

[Ext(p;X)] =def= [Σ(base(p); b. ap(Embed(X); Fiber(base(p); fam(p); b)))].

Theorem Ext_wf : [
  ∀(Polynomial; p. ∀(U<0>; X. ∈(Ext(p;X); U<1>)))
] {
  unfold <Polynomial Ext Fam base Embed CTT Fiber fam dom map fst snd>; auto;
  unfold <snd>; auto
}.

||| Fam pullbacks

Operator Pullback : (0;0;0).

[Pullback(B;f;g)] =def= [Σ(dom(f); x. Fiber(B; g; map(f;x)))].

Theorem Pullback_wf : [
  ∀(U<0>; B. ∀(Fam(B); f. ∀(Fam(B); g.
    ∈(Pullback(B;f;g); U<0>))))
] {
  unfold <Pullback Fam Fiber map dom fst snd>; auto.
}.
