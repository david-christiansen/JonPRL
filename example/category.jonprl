RawCatSig =def= [
  Σ(U<0>; Obj.
  Σ(Π(Obj; A. Π(Obj; B. U<0>)); Hom.
  Σ(Π(Obj; A.
    ap(ap(Hom; A); A)); idn.
  Σ(Π(Obj; A. Π(Obj; B. Π(Obj; C.
    Π(ap(ap(Hom; B); C); g.
    Π(ap(ap(Hom; A); B); f.
      ap(ap(Hom; A); C)))))); cmp.
  unit))))
].

obj =def= [λ(C. spread(C; x.y.x))].
hom =def= [λ(C. spread(spread(C; x.y.y); x.y.x))].
idn =def= [λ(C. spread(spread(spread(C; x.y.y); x.y.y); x.y.x))].
cmp =def= [λ(C. spread(spread(spread(spread(C; x.y.y); x.y.y); x.y.y); x.y.x))].

Theorem RawCatSig_wf : [∈(RawCatSig; U<1>)] {
  unfold <RawCatSig>; auto.
}.

Theorem obj_wf : [∀(RawCatSig; RC. ∈(ap(obj; RC); U<0>))] {
  unfold <RawCatSig>; unfold <obj>;
  auto.
}.

Theorem hom_wf : [
  ∀(RawCatSig; RC.
  ∀(ap(obj; RC); A.
  ∀(ap(obj; RC); B.
    ∈(ap(ap(ap(hom; RC); A); B); U<0>))))
] {
  unfold <RawCatSig>; unfold <hom>; unfold <obj>;
  auto.
}.

LawCatSig =def= [
  λ(RC.
  Σ(Π(ap(obj; RC); A.
    Π(ap(ap(ap(hom; RC); A); A); f.
      =(ap(ap(ap(cmp; RC); ap(idn; RC)); f);
        f;
        ap(ap(ap(hom; RC); A); A)))); law_idn_lhs.
  Σ(Π(ap(obj; RC); A.
    Π(ap(ap(ap(hom; RC); A); A); f.
      =(ap(ap(ap(cmp; RC); f); ap(idn; RC));
        f;
        ap(ap(ap(hom; RC); A); A)))); law_idn_rhs.
  Σ(Π(ap(obj; RC); A. Π(ap(obj; RC); B. Π(ap(obj; RC); C. Π(ap(obj; RC); D.
    Π(ap(ap(ap(hom; RC); A); B); f.
    Π(ap(ap(ap(hom; RC); B); C); g.
    Π(ap(ap(ap(hom; RC); C); D); h.
      =(ap(ap(ap(cmp; RC); ap(ap(ap(cmp; RC); h); g)); f);
        ap(ap(ap(cmp; RC); h); ap(ap(ap(cmp; RC); g); f));
        ap(ap(ap(hom; RC); A); D))))))))); law_cmp_ass.
   unit))))
].

CatSig =def= [subset(RawCatSig; RC. ap(LawCatSig; RC))].

law_idn_lhs =def= [λ(RC. spread(ap(LawCatSig; RC); x.y.x))].
law_idn_rhs =def= [λ(RC. spread(spread(ap(LawCatSig; RC); x.y.y); x.y.x))].
law_cmp_ass =def= [λ(RC. spread(spread(spread(ap(LawCatSig; RC); x.y.y); x.y.y); x.y.x))].
