Operator RawCat : ().
Operator obj : (0).
Operator hom : (0;0;0).
Operator idn : (0;0).
Operator cmp : (0;0;0;0;0;0).

[RawCat] =def= [
  Σ(U<0>; Obj.
  Σ(Π(Obj; A. Π(Obj; B. U<0>)); Hom.
  Σ(Π(Obj; A.
    ap(ap(Hom; A); A)); Idn.
  Σ(Π(Obj; A. Π(Obj; B. Π(Obj; C.
    Π(ap(ap(Hom; B); C); g.
    Π(ap(ap(Hom; A); B); f.
      ap(ap(Hom; A); C)))))); Cmp.
  unit))))
].

[obj(C)] =def= [spread(C; x.y.x)].
[hom(C;A;B)] =def= [ap(ap(spread(spread(C; x.y.y); x.y.x); A); B)].
[idn(C;A)] =def= [ap(spread(spread(spread(C; x.y.y); x.y.y); x.y.x); A)].
[cmp(C;X;Y;Z;f;g)] =def= [ap(ap(ap(ap(ap(spread(spread(spread(spread(C; x.y.y); x.y.y); x.y.y); x.y.x); X); Y); Z); f); g)].

Tactic rawcat_unfold {
  unfold <RawCat obj hom idn cmp>.
}.

Theorem RawCat_wf : [∈(RawCat; U<1>)] {
  refine <rawcat_unfold>; auto.
}.


Theorem obj_wf : [∀(RawCat; RC. ∈(obj(RC); U<0>))] {
  refine <rawcat_unfold>; auto.
}.

Theorem hom_wf : [
  ∀(RawCat; RC.
  ∀(obj(RC); A.
  ∀(obj(RC); B.
    ∈(hom(RC;A;B); U<0>))))
] {
  refine <rawcat_unfold>; auto.
}.

Theorem idn_wf : [
  ∀(RawCat; RC.
  ∀(obj(RC); A.
    ∈(idn(RC;A); hom(RC;A;A))))
] {
  refine <rawcat_unfold>; auto.
}.

Theorem cmp_wf : [
  ∀(RawCat; RC.
  ∀(obj(RC); X.
  ∀(obj(RC); Y.
  ∀(obj(RC); Z.
  ∀(hom(RC; Y; Z); f.
  ∀(hom(RC; X; Y); g.
    ∈(cmp(RC;X;Y;Z;f;g); hom(RC; X;Z))))))))
] {
  refine <rawcat_unfold>; auto.
}.

Operator LeftIdentity : (0).
[LeftIdentity(C)] =def= [∀(obj(C); A. ∀(obj(C); B. ∀(hom(C;A;B); f. =(cmp(C;A;B;B; idn(C;B); f); f; hom(C;A;B)))))].

Operator RightIdentity : (0).
[RightIdentity(C)] =def= [∀(obj(C); A. ∀(obj(C); B. ∀(hom(C;A;B); f. =(cmp(C;A;A;B; f; idn(C;A)); f; hom(C;A;B)))))].

Operator CmpAssoc : (0).
[CmpAssoc(C)] =def= [
  ∀(obj(C); W.
  ∀(obj(C); X.
  ∀(obj(C); Y.
  ∀(obj(C); Z.
  ∀(hom(C;W;X); f.
  ∀(hom(C;X;Y); g.
  ∀(hom(C;Y;Z); h.
    =(cmp(C;W;X;Z; cmp(C;X;Y;Z; h;g); f);
      cmp(C;W;Y;Z; h; cmp(C;W;X;Y; g;f));
      hom(C;W;Z))
  )))))))
].

Theorem LeftIdentity_wf : [∀(RawCat; RC. ∈(LeftIdentity(RC); U<0>))] {
  unfold <LeftIdentity>; refine <rawcat_unfold>; auto.
}.

Theorem RightIdentity_wf : [∀(RawCat; RC. ∈(RightIdentity(RC); U<0>))] {
  unfold <RightIdentity>; refine <rawcat_unfold>; auto.
}.

Theorem CmpAssoc_wf : [∀(RawCat; RC. ∈(CmpAssoc(RC); U<0>))] {
  unfold <CmpAssoc>; *{refine <rawcat_unfold>; auto}.
}.

Operator LawCat : (0).
[LawCat(RC)] =def= [Σ(LeftIdentity(RC); _. Σ(RightIdentity(RC); _. CmpAssoc(RC)))].

Tactic lawcat_unfold {
  unfold <LeftIdentity RightIdentity CmpAssoc LawCat>;
  refine <rawcat_unfold>.
}.

Theorem LawCat_wf : [∀(RawCat; RC. ∈(LawCat(RC); U<0>))] {
  *{refine <lawcat_unfold>; auto}.
}.

Operator Cat : ().
[Cat] =def= [subset(RawCat; C. LawCat(C))].

Tactic cat_unfold {
  unfold <Cat>; refine <lawcat_unfold>.
}.

Theorem Cat-wf : [∈(Cat; U<1>)] {
  *{refine <cat_unfold>; auto};
  elim #0; auto.
}.

Theorem InitialRawCat : [RawCat] {
  unfold <RawCat>;
  intro [void]; auto;
  intro [λ(A. λ(B. void))]; auto;
  witness [pair(λ(A. <>); pair(λ(A. <>); <>))]; auto.
}.

Theorem InitialCat : [Cat] {
  unfold <Cat>;
  intro [InitialRawCat] @0; unfold <InitialRawCat>;
  *{refine <lawcat_unfold>; auto};
  elim #0; auto.
}.

Theorem TerminalRawCat : [RawCat] {
  unfold <RawCat>;
  intro [unit]; auto;
  intro [λ(A. λ(B. unit))]; auto;
  intro [λ(A. <>)]; auto;
  intro [λ(A. λ(B. λ(C. λ(g. λ(f. <>)))))];
  auto.
}.

Theorem TerminalCat : [Cat] {
  unfold <Cat>;
  intro [TerminalRawCat] <C> @0; unfold <TerminalRawCat>;
  *{refine <lawcat_unfold>; auto};
  ?{elim #2; auto};
  elim #0; auto.
}.
